// //$Id$

/////////////////////////////////////////
// 2.0
//
// bulletinboard.src
// by Austin Heilman
// Austin@tsse.net
//
// Bulletin board script that stores posts in a
// datafile.
//
// Created: 9/04/2001 11:32PM
// Updated: 2/13/2003 12:16PM
//
// History
// Feb. 13, 2003 - Austin
// A lot of code cleanup.
// Reading posts now uses the new gumps.inc function GFHTMLArea()
//
// Sep. 04, 2001 - Austin
// Package is created.
/////////////////////////////////////////

use uo;
use os;

include ":datafile:datafile";
include ":gumps:include/old/old-gumps";

const ADD_POST_BTN := 0xE000;
const DELETE_BTN := 0xE001;

var board_data_file;
var board_elem;

program BulletinBoard(who, board)
	var input := 0;
	repeat
		board_data_file := DFOpenDataFile("MessageBoards", DF_CREATE);
		board_elem := DFFindElement(board_data_file, hex(board.serial), DF_CREATE);

		GFInitGump();

		GFButtonPage(40, 195, 0x1B6B, 0x1B6B, 500);
		GFGumpPic(20, 20, 0x87A);
		GFTextMid(260, 57, 0, GF_STDCOLOR, "Message Board");

		BoardIndex(board);

		var subject := "", msg_lines := array;
		NewPost(who, subject, msg_lines);

		input := GFSendGump(who);
		if ( !GetObjProperty(who, "#Online") )
			//If the player goes offline, terminate the script.
			exit;
		endif

		if ( input[ADD_POST_BTN] )
			subject := GFExtractData(subject.keyid, input);

			//Take the 'lines' from the post and put them into 1 string.
			var msg_string := "";
			foreach line in msg_lines
				line := GFExtractData(line.keyid, input);
				if ( line )
					msg_string := msg_string+" "+line;
				endif
				sleepms(5);
			endforeach

			var post_num := CInt(board_elem.GetProp("NextPost"));
			var new_num := CInt(board_elem.GetProp("NextPost"))+1;
			if (new_num > 50)
				//Were on post 50, reset the counter.
				new_num := 1;
			endif
			board_elem.SetProp("NextPost", new_num);
						var post_info := struct;
			post_info.+Name := who.name;
			post_info.+Subject := Subject;
			post_info.+Message := msg_string;

			board_elem.SetProp(hex(post_num), post_info);
		elseif ( input[0] >= DELETE_BTN )
			var temp := CInt(input[0]-DELETE_BTN);
			board_elem.EraseProp(hex(temp));
		endif
	until (input[0] <= 2);
	//Right click closes the gump.

endprogram

function BoardIndex(board)
	board := board;
	var y_pos := 180;
	var page_num := 1;
	var post_page_num := 1000;
	var counter := 1;
	var total := 0;
	var prop_list := board_elem.PropNames();
	foreach postnum in prop_list
		if ( postnum != "NextPost" )
			var name := board_elem.GetProp(postnum).Name;
			var subject := board_elem.GetProp(postnum).Subject;
			var message := board_elem.GetProp(postnum).Message;

			GFPage(page_num);
			GFButtonPage(135, y_pos+3, 0xFC1, 0xFC1, post_page_num);
			GFTextLine(155, y_pos, GF_STDCOLOR, subject);
			y_pos := y_pos+20;

			GFPage(post_page_num);

			var msg := "<basefont color=#111111>"
				+"From: "+name+"<br>"
				+"Subject: "+subject+"<br>"
				+"Message:<br>";

			if ( TypeOf(message) == "Array" )
				foreach line in message
					msg := msg+line+" ";
					sleepms(2);
				endforeach
			else
				msg := msg+" "+message;
			endif
			GFHTMLArea(130, 180, 263, 160, 0, 1, msg);

			post_page_num := post_page_num+1;

			GFButtonPage(135, 367, 0x15E3, 0x15E7, page_num);
			GFTextLine(155, 365, GF_STDCOLOR, "Index");

			GFTextLine(350, 364, GF_STDCOLOR, "Delete");
			GFButtonID(395, 367, 2117, 2118, 1, (CInt(postnum)+DELETE_BTN));

			counter := counter+1;
			total := total+1;

			if ( counter > 8 && total < (prop_list.size()-1) )
				GFPage(page_num);
				//Down arrow
				GFButtonPage(377, 336, 0xFC, 0xFD, page_num+1);
				page_num := page_num+1;
				GFPage(page_num);
				//Up arrow
				GFButtonPage(377, 160, 0xFA, 0xFB, page_num-1);
				y_pos := 180;
				counter := 1;
			endif
		endif
		sleepms(2);
	endforeach
endfunction

function NewPost(who, byref subject, byref msg_lines)
	GFPage(500);

	GFTextLine(130, 180, GF_STDCOLOR, "From: " + who.name);
	GFTextLine(130, 200, GF_STDCOLOR, "Subject: ");
	subject := GFTextEntry(193, 200, 105, 20, GF_STDCOLOR, "New Message");

	GFTextLine(130, 230, GF_STDCOLOR, "Message:");

	var i;
	var y_pos := 250;
	//Five message lines.
	for (i:=0; i < 5; i:=i+1)
		msg_lines.append(GFTextEntry(130, y_pos, 255, 20, GF_STDCOLOR));
		y_pos := y_pos+20;
	endfor

	GFButtonPage(135, 367, 0x15E3, 0x15E7, 1);
	GFTextLine(155, 365, GF_STDCOLOR, "Index");

	GFTextLine(355, 364, GF_STDCOLOR, "Post");
	GFButtonID(395, 367, 2117, 2118, 1, ADD_POST_BTN);
endfunction

