use uo;
use os;
use cfgfile;

include "include/client";
include "getSpellID";
include ":itemutils:canAccess";

program use_scroll(who, scroll)
  EraseObjProperty(who, "IsMeditating");
  EraseObjProperty(who, "HealTimer");
  if(!can_access(who, scroll))
    return;
  endif
  if(scroll.movable == 0)
    SendSysMessage(who, "You cannot use that.");
    return;
  endif
  if (!scroll.amount)
	return;
  endif
  if(!ReserveItem(scroll))
    return;
  endif
  if(CInt(scroll.container.objtype) == CInt(0x0EFA))
    return;  // Feel free to take this opportunity to report this
             // person as an injection user- that's the main known
             // way to try to use a scroll that is inside a spellbook.
  endif
  var spellid := GetSpellId( scroll );
  var parms := {};
  parms[1] := who;
  parms[2] := spellid;
  parms[3] := scroll;
  start_script(":spellcasting:spellcast", parms);
endprogram
