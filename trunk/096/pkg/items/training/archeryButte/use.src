use uo;
use os;
use util;
use cfgfile;

include ":attributes:attributes";
include ":combat:weaponInfo";
include ":training:training";

CONST ANIM_GET_HIT := 0x14;

program ArcheryButte(who, butte)
	if ( butte.container )
		return 0;
	endif
	
	var weapon := who.weapon;
	var weapon_attrib := CS_GetWeaponInfo(weapon, "Attribute");
	var weapon_anim := CInt(CS_GetWeaponInfo(weapon, "Anim"));
	var miss_sound := CInt(CS_GetWeaponInfo(weapon, "MissSound"));
	var hit_sound := CInt(CS_GetWeaponInfo(weapon, "Hitsound"));
	var projectile_anim := CInt(CS_GetWeaponInfo(weapon, "ProjectileAnim"));
	var projectile_type := CInt(CS_GetWeaponInfo(weapon, "ProjectileType"));
	
	if ( Distance(who, butte) == 1 )
		RecoverAmmo(who, butte);
		return 0;
	elseif ( !Lower(weapon_attrib)["archery"] )
		SendSysMessage(who, "You need to use a missile weapon.");
		return 0;
	elseif ( !TP_CheckRange(who, weapon, butte) )
		return 0;
	endif
		
	if ( !ConsumeSubstance(who.backpack, projectile_type, 1) )
		SendSysMessage(who, "You do not have any ammo for this weapon.");
		return 0;
	endif
	
	PerformAction(who, weapon_anim);
	sleep(1);
	
	PlayMovingEffect(who, butte, projectile_anim, 10, 0, 0);

	if ( SkillCheck(who, weapon_attrib, -1, 0, TP_CanAdvance(who, weapon_attrib)) > 0 )
		PlaySoundEffect(who, hit_sound);
				
		PrintTextAbove(butte, DetermineHit(who, weapon_attrib));
			
		if ( RandomInt(2) )
			set_critical(1);
			var ammo := CInt(GetObjProperty(butte, "Ammo-"+projectile_type));
			ammo := ammo + 1;
			SetObjProperty(butte, "Ammo-"+projectile_type, ammo);
			set_critical(0);
		endif
	else
		PlaySoundEffect(who, miss_sound);
	endif
	
	sleep(3);	
endprogram

function DetermineHit(who, weapon_attrib)
	if ( SkillCheck(who, weapon_attrib, RandomDiceRoll("40d3"), 0, ADV_DISABLE) > 0 )
		return "*Bullseye*";
	elseif ( SkillCheck(who, weapon_attrib, RandomDiceRoll("30d3"), 0, ADV_DISABLE) > 0 )
		return "*Hit the inner red ring*";
	elseif ( SkillCheck(who, weapon_attrib, RandomDiceRoll("20d3"), 0, ADV_DISABLE) > 0 )
		return "*Hit the inner white ring*";
	elseif ( SkillCheck(who, weapon_attrib, RandomDiceRoll("10d3"), 0, ADV_DISABLE) > 0 )
		return "*Hit the outer red ring*";
	else
		return "*It hit the straw*";
	endif
endfunction

function RecoverAmmo(who, butte)
	var prop_list := GetObjPropertyNames(butte);
	
	if ( prop_list.size() > 0 )
		foreach c_prop in prop_list
			if ( c_prop["Ammo-"] )
				var ammo := c_prop;
				ammo["Ammo-"] := "";
				
				CreateItemInBackPack(who, CInt(ammo), CInt(GetObjProperty(butte, c_prop)));
				SendSysMessage(who, "You recover some ammo.");
				
				EraseObjProperty(butte, c_prop);
			endif
			sleepms(2);
		endforeach
	else
		SendSysMessage(who, "There is no ammunition to recover from this butte.");
	endif
endfunction