// $Id$

use uo;
use util;
use os;

program Install()
	return 1;
endprogram

exported function SetOwner(corpse, owner)
	var serial;
	if ( owner.IsA(POLCLASS_MOBILE) )
		serial := owner.serial;
	else
		serial := owner;
		owner := SystemFindObjectBySerial(owner, SYSFIND_SEARCH_OFFLINE_MOBILES);
	endif
	
	SetObjProperty(corpse, "OwnerSerial", serial);
	SetObjProperty(owner, "CorpseSerial", corpse.serial);
	
	return 1;
endfunction

exported function GetOwner(corpse)
	//Function returns the corpse as the owner if no real owner can be found
	//This situation is met when:
	//The corpse belonged to an npc.
	//The player character that owned the corpse was deleted.

	var owner := CInt(GetObjProperty(corpse, "OwnerSerial"));
	if ( !owner )
		//NPC Corpse?
		return corpse;
	else
		//Return the player who owns this corpse.
		owner := SystemFindObjectBySerial(owner);
		if ( owner )
			return owner;
		else
			//Owner has been deleted - return 0
			return corpse;
		endif
	endif
	
	// Shouldnt be possible to get here.
	return corpse;
endfunction

exported function SetSkinned(corpse, skinner)
	var serial;

	if ( skinner.ISA(POLCLASS_MOBILE) )
		serial := skinner.serial;
	else
		serial := skinner;
	endif

	var corpse_name := corpse.desc;
	if ( !corpse_name["[skinned]"] )
		// Add "[skinned]" to the corpse description.
		SetName(corpse, corpse_name+" [skinned]");
	endif

	//Store the serial of who skinned the corpse.
	SetObjProperty(corpse, "Skinned", serial);
endfunction

exported function IsSkinned(corpse)
	return CInt(GetObjProperty(corpse, "Skinned"));
endfunction

exported function Gibbify(corpse, range:=10)
	var piece_list := array
	{
		0x1DAD, //Torso
		0x1DB2, //Left Leg
		0x1DB1, //Right Leg
		0x1DB0, //Left Arm
		0x1DAF, //Right Arm
		0x1CEE, //Liver
		0x1CED, //Heart
		0x1CF0  //Brain
	};
	
	foreach objtype in (piece_list)
		var item := CreateItemAtLocation(corpse.x, corpse.y, corpse.z, objtype, 1);
		
		var newx, newy;
		var num_tries := 0;

		MOVE_LOOP:
		repeat
			newx := RandomInt(range)-CInt(range/2);
			newy := RandomInt(range)-CInt(range/2);

			newx := newx+corpse.x;
			newy := newy+corpse.y;

			num_tries := num_tries+1;
			if ( num_tries > 30 )
				num_tries := 0;
				newx := corpse.x;
				newy := corpse.y;
				break MOVE_LOOP;
			endif

		until ( CheckLosAt(corpse, newx, newy, corpse.z) );
		
		MoveItemToLocation(item, newx, newy, corpse.z, MOVEITEM_FORCELOCATION);
	endforeach
	
	DestroyItem(corpse);
endfunction