// //$Id$

use uo;
use os;
use util;
use cfgfile;

include "include/canAccess";
include "include/attributes";
include ":timedscripts:timedScripts";
include "include/client";

program eat(who, food)
  EraseObjProperty(who, "IsMeditating");
  EraseObjProperty(who, "HealTimer");

  if (!can_access(who, food))
    return;
  endif
  if (food.movable == 0)
    SendSysMessage(who, "You cannot use that.");
    return;
  endif
  if (!ReserveItem(food))
    SendSysMessage(who, "Someone is doing something with that.");
    return;
  endif

  var foodcfg := ReadConfigFile(":*:itemdesc");
  var themsg;
  var poison := CInt(GetObjProperty(food, "poison_level"));
  var foodvalue := foodcfg[food.objtype].foodvalue;
  var hunger := CInt(GetObjProperty(who, "hunger"));
  if (!foodvalue)
    foodvalue := 1;
  endif
  if (hunger == 0)
    SendSysMessage(who, "You are too full to eat any more.");
    return;
  endif

  if (SubtractAmount(food, 1))
    case (RandomInt(3) + 1)
      1: PlaySoundEffect(who, 0x3b);
      2: PlaySoundEffect(who, 0x3c);
      3: PlaySoundEffect(who, 0x3d);
    endcase
    PerformAction(who, ANIM_EAT);
    SetStamina(who, GetStamina(who) +(RandomInt(5)+foodvalue));
    if (GetStamina(who) > GetDexterity(who))
      SetStamina(who, who.dexterity);
    endif
    hunger := hunger - foodvalue;
    case (hunger)
      0:
      1:  themsg := "You manage to eat the food, but you are stuffed.";
      2:
      3:  themsg := "You feel quite full after consuming the food.";
      4:
      5:
      6:
      7:  themsg := "After eating the food you feel much less hungry.";
      8:
      9:
      10: themsg := "You eat the food but are still extremely hungry.";
    endcase
    if (hunger < 0)
      themsg := "You manage to eat the food, but you are stuffed";
      hunger := 0;
    endif
    SetObjProperty(who, "hunger", hunger);
    case (hunger)
      0:  SetStaminaRegenRate(who, 1000);
      1:  SetStaminaRegenRate(who, 1300);
      2:
      3:
      4:
      5:
      6:  SetStaminaRegenRate(who, 1200);
      7:  SetStaminaRegenRate(who, 1000);
      8:  SetStaminaRegenRate(who, 800);
      9:  SetStaminaRegenRate(who, 600);
      10: SetStaminaRegenRate(who, 400);
    endcase
    SendSysMessage(who, themsg);
    if (poison > 0)
      TS_StartTimer(who, "defaultPoison", 120, poison, who);
    endif
  endif
endprogram
