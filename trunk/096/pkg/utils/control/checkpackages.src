use uo;
use os;
use cfgfile;

include ":datafile:datafile";
include "include/string";


// Run 255 lines before going to the next script.
// Its as close as it gets to critical without the console whining about it.
set_priority(255);
set_script_option(SCRIPTOPT_NO_RUNAWAY, 1);

var pkg_info := DFOpenDataFile(":control:packages", DF_CREATE);
var pkg_gumps := DFOpenDataFile(":control:gumps", DF_CREATE);

program CheckPackages()

	// This foreach loop is here until DeleteDataFile() in datafile.em works.
	// Its purpose is to wipe the datafile so its updated for the new package settings.
	foreach elem_name in (pkg_info.keys())
		pkg_info.DeleteElement(elem_name);
	endforeach
	pkg_gumps.DeleteElement("index");
	
	DF_DEBUG_MODE := 0;

	// Run through each package
	// If it has an ICP.cfg in its 'configs' folder, get the settings we want from it.
	// If not, itll go on the gump as packagename [No ICP support]
	foreach package in (polcore().packages);
		var temp_info := struct;

		var data_elem := DFFindElement(pkg_info, package, DF_CREATE);

		var icp_cfg := ReadConfigFile(":"+package+":config/icp");
		icp_cfg := icp_cfg["Register"];

		if ( icp_cfg )
			temp_info.+Name := GetCfgValue(icp_cfg, "Name");
			temp_info.+Version := CDbl(GetCfgValue(icp_cfg, "Version"));
			temp_info.+Description := GetCfgValue(icp_cfg, "Description");
			temp_info.+Creator := GetCfgValue(icp_cfg, "Creator");
			temp_info.+C_Email := GetCfgValue(icp_cfg, "C_Email");
			temp_info.+Maintainer := GetCfgValue(icp_cfg, "Maintainer");
			temp_info.+M_Email := GetCfgValue(icp_cfg, "M_Email");

			// Makes a list of scripts and text commands that will be on the gump.
			var script_list := GetConfigStringArray(icp_cfg, "Script");
			var cmd_list := GetConfigStringArray(icp_cfg, "TextCmd");

			data_elem.SetProp("Main", temp_info);
			data_elem.SetProp("Scripts", script_list);
			data_elem.SetProp("TextCommands", cmd_list);
		else
			temp_info.+Name := package;
			temp_info.+NoSupport := 1;
			data_elem.SetProp("Main", temp_info);
		endif

		UnloadConfigFile(":"+package+":config/icp");
	endforeach

	Print("[CONTROL] Finished setting up packages.");
endprogram

// * GetCfgValue(byref cfg_elem, value)
//
// Used to get values from the config file.
// Makes sure that the value returned is "clean"
// so <uninitialized object> is not seen on the gump.
//
function GetCfgValue(byref cfg_elem, value)
	value := GetConfigString(cfg_elem, value);
	if ( !value )
		value := "";
	endif
	return value;
endfunction
