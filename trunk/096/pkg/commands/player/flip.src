////////////////////////////////////////////////////////////////////////////////////////////////////
//
//  name:    flip.src - Player Textcommand
//  version: 1.0a
//  author:  Distro
//
//  Purpose: Changes the orientation of a piece of furniture
//
////////////////////////////////////////////////////////////////////////////////////////////////////
use uo;
//use basic;
use cfgfile;

program flip (me)

	var furniture := 0;

	if( TypeOf(me) == "Array" )
		furniture := me[2];
		me := me[1];
	endif

	if( !furniture )
		SendSysMessage(me, "Select the furniture you wish to re-orient.");
		furniture := Target(me);

		if ( !furniture.isA(POLCLASS_ITEM) )
			SendSysMessage(me, "You must select an item!");
			return 0;
		endif

		if (furniture.movable == 0)
			SendSysMessage(me, "That item is locked down.");
			return 0;
		endif
	endif

	var flipConfig  := ReadConfigFile(":commands:config/flip");
	var flipGraphic := flipConfig[CInt(furniture.graphic)].ChangeTo;

	if( !flipGraphic )
		SendSysMessage(me, "That item cannot be re-orientated.");
		return 0;
	endif

	furniture.graphic := flipGraphic;

endprogram
