/*===============================================================
* Current Version
* SETPROP.SRC - v5.0
* Updated 9/13/2005 7:15AM
*
* -- Revision v1.0 --
* Austin:
*  Created include file
===============================================================*/

use uo;

program SetProperties(who, text)
	var param := SplitWords(text);
	if ( param.size() < 2 )
		SendSysMessage(who, "SYNTAX: .setprop <property name> <value>");
		return;
	endif

	var targ := target(who);
	if ( !targ )
		SendSysMessage(who, "Cancelled");
		return;
	endif

	var property := Lower(param[1]);
	var pos := Find(text, param[2], Len(param[1])+1);
	var value := text[pos, Len(text)];

	// Useful for clearing something like 'mobile.guild_title'.
	if ( value == "NULL" )
		value := "";
	endif

	ProcessInput(who, targ, property, value);
endprogram

function ProcessInput(who, targ, property, value)
	var old_val := targ.get_member(property);
	var old_type := Lower(TypeOf(old_val));
	case ( old_type )
		"integer"	: value := CInt(value);
		"string"	: value := CStr(value);
		"double"	: value := CDbl(value);
	endcase

	SendSysMessage(who, "Old Value->"+old_val);

	// Handles situations where methods or functions must be used.
	case ( property )
		"x": "y": "z": "realm": 
			DoMovement(targ, property, value);
			break;
		"concealed":
			DoConceal(who, targ, value);
			break;
		"cmdlevel":
			DoCmdLevel(who, targ, value);
			break;
		"criminal":
			targ.SetCriminal(value);
			break;
		"murderer":
			targ.SetMurderer(value);
			break;
		default:
			targ.set_member(property, value);
			break;
	endcase
	
	SendSysMessage(who, "New Value->"+targ.get_member(property));
endfunction

function DoMovement(targ, property, value)
	var x := targ.x;
	var y := targ.y;
	var z := targ.z;
	var realm := targ.realm;

	if ( property == realm )
		MoveObjectToRealm(targ, property, x, y, z);
	else
		case ( property )
			"x": x := value;
			"y": y := value;
			"z": z := value;
		endcase

		if ( targ.IsA(POLCLASS_ITEM) )
			MoveItemToLocation(targ, x, y, z, MOVEITEM_FORCELOCATION);
		elseif ( targ.IsA(POLCLASS_MOBILE) )
			MoveCharacterToLocation(targ, x, y, z, MOVECHAR_FORCELOCATION);
		endif
	endif
endfunction

function DoCmdLevel(who, targ, value)
	if ( value > who.cmdlevel )
		value := who.cmdlevel;
		SendSysMessage(who, "You can not set a command level higher than your own.");
	endif

	targ.cmdlevel := value;
endfunction

function DoConceal(who, targ, value)
	if ( value > who.cmdlevel )
		value := who.cmdlevel;
		SendSysMessage(who, "You can not conceal something higher than your cmdlevel.");
	endif

	targ.concealed := value;
endfunction
