/*===============================================================
* Current Version
* BRAIN.SRC - v5.0
* Updated 9/11/2005 2:06AM
*
* NPC AI-Script to control all non-setup actions. Acts as the "brain"
* and fires off external scripts that act as the "nerves" of the NPC.
*
* -- Revision v1.0 --
* Austin:
*  Created include file
===============================================================*/

use uo;
use os;
use npc;
use cfgfile;
use polsys;

include "include/sysEvent";
include ":brainAI:include/braincore/error";
include ":brainAI:include/braincore/cycleScript";
include ":brainAI:include/braincore/eventProcessor";
include ":brainAI:include/braincore/movement";
include ":brainAI:include/braincore/setup";
include ":brainAI:include/braincore/sleep";
include ":brainAI:include/eventid";
include ":brainAI:include/npcCommands";
include ":brainAI:include/npcNerves";
include ":brainAI:include/npcSettings";
include ":brainAI:include/npcUtil";

//
// * Global variables
//
var npc := Self();
var idle_ticks := 0;
var sleep_mode := 0;

//Would be nice in future pol releases if these could be handled
//as references rather than copies within the brain and nerves.
var g_settings := dictionary; //"setting name"->value
var g_scripts := dictionary; // "script type"->value{.flags, .script}

program BrainAI()
	//Run setup steps for the npc.
	set_critical(1);
	InitializeSettings();
	InitializeEvents();
	set_critical(1);
	StartupScripts();
	
	sleep(1);
	
	while ( npc )
		var ev;
		if ( !sleep_mode )
			RunCycleScript();			
			ev := Wait_For_Event(g_settings["CycleWait"]);
		else
			ev := Wait_For_Event(g_settings["SleepWait"]);
		endif
		
		if ( !ev )
			if ( !sleep_mode )
				idle_ticks:=idle_ticks+1;
			endif
			if ( ShouldSleep() )
				EnterSleepMode();
			endif
		else
			if ( ev.WakeUp != NOWAKE )
				LeaveSleepMode();
			endif
			
			ProcessEvent(ev);
		endif
	endwhile
endprogram
