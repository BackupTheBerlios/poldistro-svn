use uo;
use os;

include ":brainAI:eventid";
include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":brainAI:merchantUtil";

program BrainNerve(params)
	var npc		:= params[1];
	//var nerve_name:= params[2];
	var event	:= params[3];
	//var settings	:= params[4];
	//var scripts	:= params[5];
	params := 0; // Not needed anymore.
	
	while ( npc )
		if ( event )
			case ( event.type )
				SYSEVENT_SPEECH:
					SpeechEvent(npc, event);
					break;
				default:
					break;
			endcase
			
			event := 0;
		endif
		
		sleepms(5);
		event := wait_for_event(900000);
	endwhile
endprogram

function SpeechEvent(npc, event)
	AI_Speak(npc, "I just heard "+(event.source).name+" say \""+event.text+"\"", SPEAK_TEXTTYPE_DEFAULT, SPEAK_DOEVENT_DISABLE, WAKEUP);
	
	// Doing 'SPEAK_DOEVENT_ENABLE' here with multiple npcs around, could cause an infinite loop - they would keep talking to each other!
	case ( event.texttype )
		SPEAK_TEXTTYPE_YELL:
			AI_Speak(npc, "*yells* No need to yell!", event.texttype, SPEAK_DOEVENT_DISABLE, WAKEUP);
			break;
		SPEAK_TEXTTYPE_WHISPER:
			AI_Speak(npc, "*whispers* Speak up next time. I could barely hear you.", event.texttype, SPEAK_DOEVENT_DISABLE, WAKEUP);
			break;
		default:
			AI_Speak(npc, "You're fun to talk to, you know that?", SPEAK_TEXTTYPE_DEFAULT, SPEAK_DOEVENT_DISABLE, WAKEUP);
			break;
	endcase
	
	if ( event.text["restock"] )
		CreateItemInContainer(NPC_MerchantForSaleContainer(npc), "longsword", 1);
	elseif ( event.text["buy"] )
		BuyStuff(npc, event.source);
	endif
	
	return 1;
endfunction

function BuyStuff(npc, mobile)
	var forsale_bp := NPC_MerchantForSaleContainer(npc);
	var shopping_cart := NPC_MerchantShoppingCartContainer(npc);
	
	AI_Turn(npc, mobile, NETURN_TOWARD);
		
	var result := SendBuyWindow(mobile, forsale_bp, npc, shopping_cart);
	if ( !result )
		PrintTextAbove(npc, "SendBuyWindow() error - "+result.errortext);
	endif
	
	return 1;
endfunction