use uo;
use os;

include ":brainAI:include/npcNerves";
include ":brainAI:include/npcCommands";
include ":brainAI:include/npcWatchList";

program BrainNerve(params)
	var npc		:= params[1];
	//var nerve_name	:= params[2];
	var event	:= params[3];
	var settings	:= params[4];
	var scripts	:= params[5];
	params := 0; // Not needed anymore.
	
	while ( npc )
		if ( event )
			case ( event.type )
				SYSEVENT_ENTEREDAREA:
					EnteredArea(npc, event, settings, scripts);
					break;
				default:
					break;
			endcase
			
			event := 0;
		endif
		
		sleepms(5);
		event := wait_for_event(600);
	endwhile
endprogram

function EnteredArea(npc, byref event, byref settings, byref scripts)
	var source := event.source;
	
	if ( NPC_ShouldWatch(npc, source, settings, scripts) )
		if ( NPC_AddToWatchList(npc, source) )
			AI_WakeUp(npc);
			AI_Speak(npc, "Ive added you to my watch list "+source.name);
			
			return 1;
		endif
	endif
	
	return 0;
endfunction
