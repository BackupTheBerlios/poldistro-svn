use uo;
use os;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":brainAI:npcWatchList";

program BrainNerve(params)
	var npc		:= params[1];
	//var nerve_name	:= params[2];
	var event	:= params[3];
	var settings	:= params[4];
	var scripts	:= params[5];
	params := 0; // Not needed anymore.
	
	while ( npc )
		if ( event )
			CHECKS: case ( event.type )
				SYSEVENT_ENTEREDAREA:
					EnteredArea(npc, event, settings, scripts);
					break CHECKS;
				default:
					break;
			endcase
			
			event := 0;
		endif
		
		event := wait_for_event(600);
	endwhile
endprogram

function EnteredArea(npc, byref event, byref settings, byref scripts)
	var source := event.source;
	
	if ( NPC_ShouldWatch(npc, source, settings, scripts) )
		if ( NPC_AddToWatchList(npc, source) )
			AI_WakeUp(npc);
			
			if ( scripts["LookAround"] )
				if ( !GetNerveProcess(npc, "LookAround") )
					AI_WakeUp(npc);
				endif
			endif
			
			return 1;
		endif
	endif
	
	return 0;
endfunction
