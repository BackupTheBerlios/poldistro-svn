use npc;
use os;
use uo;
use cfgfile;

include "include/client";
include ":attributes:attributes";
include "include/eventID";
include "include/sysEvent";
include ":npcutils:names";
include ":npcutils:vetement";
include "include/dist";
include ":npcutils:anchors";
include ":npcutils:NPCBackpacks";
include ":begging:begging";
include ":npcutils:trainSkill";
include ":timedscripts:timedScripts";

var flee_point := 0;
var npccfg := ReadConfigFile("::npcdesc");

const HALT_THRESHOLD := 10;
var me := Self();

var cfgfile     := ReadConfigFile("::gzone");
var keys        := GetConfigStringKeys(cfgfile);
var next_wander := ReadGameClock() + 30;
var nextchk     := ReadGameClock() + 120;
var wanders     := 9;
var myanchor;

program CastleGuard()
  SetObjProperty(me, "MerchantType", "TownGuard");
  start_script(":npcutils:NPCKeeper", me);
  myanchor := GetObjProperty(me, "Anchor");
  if(myanchor)
    MoveCharacterToLocation(me, myanchor[1], myanchor[2], myanchor[3], MOVECHAR_FORCELOCATION);
  endif
  drop_anchor();
  myanchor := GetObjProperty(me, "Anchor");
  EnableMainEvents();
  EnableEvents(SYSEVENT_SPEECH, 2);
  SetWarMode(0);
  set_priority(100);
  var ev, txt;
  var myname := lower(SplitWords(me.name)[1]);
  while (1)
    ev := os::wait_for_event(30);
	if(ev)
      case(ev.type)
        EVID_NODE:           foreach thing in (ev.crims)
                               Fight(thing);
                             endforeach
        SYSEVENT_SPEECH:     txt := lower(ev.text);
                             if(txt[myname] && (txt["train"] || txt["teach"]))
                               TurnToward(ev.source);
                               MerchantTrain(me, ev.source, ev.text);
                             endif
        SYSEVENT_ENGAGED:    if((ev.source) && (!ev.source.dead))
		                       say("Thou shalt regret thine actions, swine!");
                               Fight(ev.source);
                             endif
        SYSEVENT_DAMAGED:    if((ev.source) && (!ev.source.dead))
		                       say("Thou shalt regret thine actions, swine!");
                               Fight(ev.source);
                             endif
        SYSEVENT_ITEM_GIVEN: TrainSkill(me, ev.source, ev.item);
      endcase
	endif
	if(ReadGameClock() >= next_wander)
	  begpurse(me);
      WanderMe();
      next_wander := ReadGameClock() + 10;
    endif
    if(ReadGameClock() > nextchk)
      lookiehere();
      nextchk := ReadGameClock() + 120;
    endif
  endwhile
endprogram

function WanderMe()
  wander();
  if(coordist(me.x, me.y, myanchor[1], myanchor[2]) > 15)
    MoveCharacterToLocation(me, myanchor[1], myanchor[2], myanchor[3], MOVECHAR_FORCELOCATION);
  endif
endfunction

function lookiehere()
  foreach npc in ListMobilesNearLocation(me.x, me.y, me.z, 15, me.realm)
    foreach listing in keys
      var range := SplitWords(cfgfile[listing].range);
      if((npc.x >= CInt(range[1])) && (npc.x <= CInt(range[3])) && (npc.y >= CInt(range[2])) && (npc.y <= CInt(range[4])))
        var timer := Cint(GetObjProperty(npc, "guardstimer"));
      	if((npc.criminal) and (!npc.dead))
          if(timer < ReadGameClock())
            SetObjProperty(npc, "#guardstimer", ReadGameClock() + 15);
      	    Fight(npc);
          endif
      	elseif((npc.script == "tamed") || (npc.script == "employed"))
      	  var master := GetObjProperty(npc, "master");
      	  if(master)
      	    if(SystemFindObjectBySerial(master, SYSFIND_SEARCH_OFFLINE_MOBILES).criminal)
              if(timer < ReadGameClock())
                SetObjProperty(npc, "#guardstimer", ReadGameClock() + 15);
      	        Fight(npc);
              endif
            endif
          endif
        elseif((!npccfg[npc.npctemplate].guardignore) and (npc.isA(POLCLASS_NPC)))
          if(timer < ReadGameClock())
            SetObjProperty(npc, "#guardstimer", ReadGameClock() + 15);
      	    Fight(npc);
          endif
        endif
        break;
      endif
      sleepms(10);
    endforeach
    sleepms(10);
  endforeach
endfunction

function Fight(opponent)
  SetAnchor( me.x, me.y, 20, 0 );
  if(opponent.isA(POLCLASS_NPC))
	SetObjProperty(opponent, "guardkill", 1);
  endif
  if((opponent.cmdlevel > 0) || (opponent.serial == me.serial))
    SetWarMode(0);
    opponent := 0;
	return;
  endif
  var parms := {};
  parms[1] := me.x;
  parms[2] := me.y;
  parms[3] := me.z;
  SetObjProperty(me, "StartCoords", parms);
  var tries := 0;
  PlayStationaryEffect(me.x, me.y, me.z, 0x3728, 0xa, 0xa, me.realm);
  while(((Distance(me, opponent) > 7) || (!CheckLineOfSight(me, opponent))) and (tries < 5))
    var newx := opponent.x + RandomInt(4) - RandomInt(4);
    var newy := opponent.y + RandomInt(4) - RandomInt(4);
    sleepms(500);
    MoveCharacterToLocation(me, newx, newy, opponent.z, MOVECHAR_FORCELOCATION);
    tries := tries + 1;
  endwhile
  PlayStationaryEffect(me.x, me.y, me.z, 0x3728, 0xa, 0xa, 0, me.realm);
  PlaySoundEffect(me, 0x01ff);
  PrintTextAbove(me, "Thou shalt regret thine actions, swine!");
  SetObjProperty(opponent, "guardkill", 1);
  set_priority(50);
  if(tries >= 5)
    PerformAction(me, 13);
    ApplyRawDamage(opponent, GetHP(opponent) + 10);
    return;
  endif
  SetOpponent(opponent);
  prepare_for_fight(opponent);
  TurnToward(opponent);
  var loops := 0;
  var ev;
  var waittime := 0;
  while((opponent) && (!opponent.dead) && (!opponent.hidden) && (!opponent.concealed) && (dist(me,opponent) < 20))
	if(!CloseDistance(opponent))
	  loops := loops + 1;
	  waittime := 0;
	else
	  loops := 0;
	  waittime := 1;
	endif
	in_combat_event_loop(opponent, loops);
	ev := wait_for_event(waittime);
  endwhile
  sleep(2);
  post_combat();
  return;
endfunction

function post_combat()
  DisableCombatEvents();
  EnableMainEvents();
  SetWarMode(0);
  SetOpponent(0);
  var allpoisons := TS_GetTimerNames(me, "P");
  if(allpoisons.size() > 0)
    sleep(1);
    PerformAction(me, 0x22);
    foreach poison in allpoisons
      TS_LowerDuration(me, poison, -1);
    endforeach
    PlaySoundEffect(me, 0x31);
    sleep(1);
  endif
  if(AP_GetVital(me, "Hits") < AP_GetVitalMaximumValue(me, "Hits"))
    sleep(1);
    PerformAction(me, 0x22);
    SetHp(me, AP_GetVitalMaximumValue(me, "Hits"));
    PlaySoundEffect(me, 0x32);
    sleep(1);
  endif
  lookiehere();
  sleep(1);
  RestartScript(me);
endfunction

function CloseDistance( opponent )
  case (Distance( me, opponent ))
    1:
    0:       return 1;
    default: RunToward( opponent );
             return 0;
  endcase
endfunction

function CheckForCriminals(jerk)
  if(jerk.dead)
	return;
  endif
  if(jerk.criminal)
	say("Thou wilt regret thine actions, swine!");
	Fight(jerk);
  endif
endfunction

function cleareventque()
  var ev;
  repeat
  until(!(ev := os::wait_for_event(0)));
endfunction

function prepare_for_fight(opponent)
  opponent := opponent;
  DisableMainEvents();
  EnableCombatEvents();
endfunction

function EnableMainEvents()
    cleareventque();
    DisableEvents(SYSEVENT_DISENGAGED);
    EnableEvents(SYSEVENT_ENGAGED + SYSEVENT_DAMAGED);
    EnableEvents(SYSEVENT_ITEM_GIVEN);
endfunction

function DisableMainEvents()
    cleareventque();
    DisableEvents(SYSEVENT_ENGAGED + SYSEVENT_DAMAGED );
    DisableEvents(SYSEVENT_ITEM_GIVEN);
endfunction

function EnableCombatEvents()
    EnableEvents( SYSEVENT_ENGAGED + SYSEVENT_DAMAGED + EVID_PEACEMADE );
endfunction

function DisableCombatEvents()
    DisableEvents( SYSEVENT_ENGAGED + SYSEVENT_DAMAGED + EVID_PEACEMADE );
endfunction

function in_combat_event_loop(opponent, loops)
  opponent := opponent;
  var allpoisons := TS_GetTimerNames(me, "P");
  if(allpoisons.size() > 0)
    sleep(1);
    PerformAction(me, 0x22);
    foreach poison in allpoisons
	TS_LowerDuration(me, poison, -1);
    endforeach
    PlaySoundEffect(me, 0x31);
    sleep(1);
  endif
  if(AP_GetVital(me, "Hits") < (GetStrength(me) / 2))
    sleep(1);
    PerformAction(me, 0x22);
    SetHp(me, me.strength);
    PlaySoundEffect(me, 0x32);
    sleep(1);
  endif
  if(loops > 50)
	RestartScript(me);
	return;
  endif
endfunction