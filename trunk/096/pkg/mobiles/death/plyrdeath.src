/* $Id$
 *
 * Purpose
 * This script allows side effects to be triggered as a result of Player death,
 * like unmounting players off their mount, allowing ghosts to report their murderer,
 * Auto-Resurrect choices, etc.
 *
 */
use uo;
use os;
use util;

include "include/OLD/noto";
include "include/reportMurder";
include ":attributes:attributes";
include ":timedscripts:timedScripts";
include ":death:death";
include ":mounts:mounts";

program core_chrDeath(params)
	var corpse := params[1];
	var ghost := params[2];
	
	TS_Death(ghost);
	
	EraseObjProperty(ghost, "IsMeditating");
	
	DP_PlayDeathSound(ghost);
	MP_DeathUnmount(corpse);
	
	CheckForAutoRes(ghost, corpse);
	
	if ( (ghost.reportables).Size() > 0)
		SendReportGump(ghost);
	endif
	
	return 1;
endprogram

function NotorietyChecks(ghost)
	var killer := GetObjProperty(ghost, "LastHit");
  	if (killer != error)
		AdjustNoto((SystemFindObjectBySerial(killer[2])), ghost);
	endif
	var fame := CInt(GetObjProperty(ghost, "Fame"));
	fame := (fame - CInt(fame / 20));
	SetObjProperty(ghost, "Fame", fame);
	SetObjProperty(corpse,"serial", ghost.serial);
	
	return 1;
endfunction

function CheckForAutoRes(who, corpse)
	if( CInt(GetObjProperty(corpse, "AutoRes")) >= 1 )
		Resurrect(who);
		AP_RefreshVitals(who);	
		
		if ( !who.backpack )
			return 1;
		endif
		
		foreach item in (EnumerateItemsInContainer(corpse))
			if ( item.container == corpse )
				if ( !EquipItem(who, item) )
					MoveItemToContainer(item, who.backpack);
				endif
			endif
		endforeach
		
		return 1;
	endif
	return 0;
endfunction

