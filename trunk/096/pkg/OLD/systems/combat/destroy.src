use uo;
use os;
use cfgfile;

include "include/client";
include ":attributes:attributes";

const EVID_UNEQUIP   := 0x123456;
const EVID_DISCHARGE := 0x123457;

program destroy(item)
  set_critical(1);
  var ser := GetObjProperty(item, "#equipped");
  var who := SystemFindObjectBySerial(cint(ser), SYSFIND_SEARCH_OFFLINE_MOBILES);
  var itemdesc := ReadConfigFile("itemdesc");
  var penalty := CInt(itemdesc[item.objtype].DexPenalty);
  if(penalty)
    EraseObjProperty(item, "#equipped");
    AP_SetStatMod(who, DEXTERITY, AP_GetStatMod(who, DEXTERITY) + penalty);
  endif
  var type := GetObjProperty(item,"type");
  var val := 0;
  case(type)
    "accuracy":     val := accuracy(who,item);
    "cursed":       val := cursed(who,item);
    "sight":        val := sight(who,item);
    "reflect":      val := reflect(who,item);
    "protection":   val := protection(who,item);
    "wizzohat":     val := wizzohat(who,item);
    "strength":     val := ModifyStrength(who,item);
    "invisibility": val := invisibility(who, item);
    default:        val := 1;
  endcase
  return val;
  set_critical(0);
endprogram

function ModifyStrength(who,item)
  who := who;
  item := item;
  return 1;
endfunction

function wizzohat(who,item)
  AP_SetStatMod(who, INTELLIGENCE, AP_GetStatMod(who, INTELLIGENCE) - 10);
  AP_SetStatMod(who, STRENGTH, AP_GetStatMod(who, STRENGTH) + 5);
  AP_SetStatMod(who, DEXTERITY, AP_GetStatMod(who, DEXTERITY) + 5);
  EraseObjProperty(item, "#equipped");
  return 1;
endfunction

function cursed(who,item)
  who := who;
  item := item;
  return 0;
endfunction

function invisibility(who,item)
  RemoveMods(who, item);
  return 1;
endfunction

function sight(who,item)
  RemoveMods(who, item);
  return 1;
endfunction

function reflect(who,item)
  RemoveMods(who, item);
  return 1;
endfunction

function accuracy(who,item)
  var amt := CInt(GetObjProperty(item,"gain"));
  amt := (0 - amt);
  if((!amt) || (amt == 0))
    amt := (0 - 5);
  endif
  var cfg := ReadConfigFile(":combat:itemdesc");
  var attrib := cfg[item.objtype].Attribute;
  case(attrib)
    "Wrestling": AP_ModifySkillMod(who, WRESTLING,     amt);
    "Fencing":   AP_ModifySkillMod(who, FENCING,       amt);
    "Archery":   AP_ModifySkillMod(who, ARCHERY,       amt);
    "Swords":    AP_ModifySkillMod(who, SWORDSMANSHIP, amt);
    "Mace":      AP_ModifySkillMod(who, MACE_FIGHTING,  amt);
  endcase
  EraseObjProperty(item, "#equipped");
  return 1;
endfunction

function protection(who,item)
  RemoveMods(who, item);
  return 1;
endfunction

function RemoveMods(who, item)
  who := who;
  var proc := GetProcess(Cint(GetObjProperty(item, "Pid")));
  if(proc)
    var ev := {};
    ev.+type := EVID_UNEQUIP;
    proc.sendevent(ev);
  endif
  EraseObjProperty(item, "#equipped");
endfunction
