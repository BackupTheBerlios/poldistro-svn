// //$Id$

////////////////////////////////////////////////////////////////////////////////////////////////////
//
//  name:    stealth.src - Skill Script
//  version: 1.0a
//  author:  MuadDib
//
//  Purpose: Follows OSI methodology for the Stealthing skill with a bit of customness. If player
//		has at least 80 skill in hiding, they can stealth when hidden. If they have 80 hiding
//		and at least 80 stealth, they can try to hide and stealth in one step. Hiding script
//		is fired if they succeed in attempt to stealth, with over 80 hiding and stealth.
//
////////////////////////////////////////////////////////////////////////////////////////////////////
use uo;

include ":attributes:include/attributeConstants";
include ":attributes:include/attributes";
include ":attributes:include/skillCheck";

program skill_Stealth( who )

	EraseObjProperty(who, "IsMeditating");
	EraseObjProperty(who, "HealTimer");

	if( AP_GetSkill(who, ATTRIBUTE_HIDING) < 80 )
		PrintTextAbovePrivate( who, "You are not good enough at Hiding to do that.", who );
		return 0;
	endif
	if( (!who.hidden) && (AP_GetSkill(who, ATTRIBUTE_STEALTH) < 80) )
		PrintTextAbovePrivate( who, "You must be hidden to use stealth", who );
		return 0;
	endif
	var mount := GetEquipmentByLayer(who, 25 );
	if(mount)
		who.hidden := 0;
		PrintTextAbovePrivate( who, "You can't stealth while riding a horse!", who );
		return 0;
	endif

	var my_Skill := AP_GetSkill(who, ATTRIBUTE_STEALTH) / 10;
	if(SkillCheck(who, ATTRIBUTE_STEALTH, -1, (my_Skill*10)) > 0)
		if( !who.hidden )
			// Used RSTC because it runs the script in critical mode, thus
			// ensuring the hiding attempt completes before the stealth
			// script continues.
			Run_Script_To_Completion(":hiding:hiding", who);
			if ( !who.hidden )
				SendSysMessage(who, "You must be hidden to stealth.");
				return 0;
			endif
		endif
		who.stealthsteps := CInt(my_Skill + RandomInt(my_Skill));
		PrintTextAbovePrivate( who, "You are now moving stealthily.", who );
		return 1;
	else
		if( who.hidden )
			who.hidden := 0;
			PrintTextAbovePrivate( who, "You have revealed yourself!", who );
		else
			PrintTextAbovePrivate( who, "You fail to move stealthily!", who );
		endif
		return 0;
	endif

endprogram