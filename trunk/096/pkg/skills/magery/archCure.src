use uo;
use cfgfile;

include "include/eventID";
include "include/sound";
include "include/tileEffects";
include "include/attributes";
include ":poisonwatcher:poisons";

program archCure(parms)
  var caster := parms.caster;
  var castOn := parms.castOn;
  var npccfg := ReadConfigFile("::npcdesc");
  PlaySoundEffect(caster, SFX_SPELL_ARCH_CURE);
  var magery := GetEffectiveSkill(caster, SKILLID_MAGERY);
  var potency := CInt(GetEffectiveSkill( caster, SKILLID_MAGERY ) /33)+1;
  foreach castOn in ListMobilesNearLocation( castOn.x, castOn.y, castOn.z, CInt(magery/15), caster.realm );
    if((CheckLineOfSight(castOn, caster)) and (castOn.cmdlevel < 2))
	  PlayObjectCenteredEffect( castOn, FX_BLESS_EFFECT, 10,10);
      if(castOn.isA(POLCLASS_NPC))
        var element := npccfg[castOn.npctemplate].elemental;
        if(element == "poison")
          var ev := array;
          ev.+ type;
          ev.+ source;
          ev.type := EVID_PEACEMADE;
          SendEvent(castOn, ev);
          ev.source := caster;
          SendEvent(castOn, ev);
          ev.type := EVID_ENGAGED;
          SendEvent(castOn, ev);
          ApplyRawDamage(castOn, potency);
        endif
      endif
      if (ListPoisonsByType(castOn, "defaultPoison").size() > 0)
        CurePoison(castOn, 180, "defaultPoison", potency);
        if (ListPoisonsByType(castOn, "defaultPoison").size() > 0)
          SendSysMessage(caster, "You cure some poison.");  
          SendSysMessage(castOn, caster.name + " has partially cured you!");
        else
          SendSysMessage(caster, castOn.name + " cured.");
          SendSysMessage(castOn, caster.name + " has cured you completely!");
        endif
      endif
    endif
  endforeach
endprogram