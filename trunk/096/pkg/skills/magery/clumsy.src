use uo;
use os;
use util;
use cfgfile;

include "include/statMod";
include "include/attributes";
include "include/sound";
include "reflect";
include "include/magic";

program clumsy( parms )
  var caster := parms.caster;
  var castOn := parms.castOn;
  var spellid := parms.spellid;
  var spellcfg := ReadConfigFile(":*:spells");
  var circle := spellcfg[spellid].circle;

  SetObjProperty(castOn, "LastHit", {caster.name,caster.serial, "clumsy" });
  if(spell_reflect(castOn))
	castOn := caster;
  endif
  PlaySoundEffect(castOn, SFX_SPELL_CURE);
  PlayObjectCenteredEffect( castOn, FX_CURSE_EFFECT, 7,7);
  if(!CanMod(castOn, "dex"))
	SendSysMessage(caster,"Already under the influence!");
	return;
  endif
  var magery := GetEffectiveSkill(caster, SKILLID_MAGERY);
  var modAmount := Resisted(circle,caster,castOn,(GetModAmount( magery )));
  var duration := GetModDuration( magery );
  DoTempMod(castOn, "dex", 0 - modAmount, duration);
endprogram