use uo;

include "include/magic";
include "include/sound";
include "include/tileEffects";
include ":magery:reflect";
include ":attributes:attributes";


program spell_manaVamp(parms)
  var caster := parms.caster;
  var castOn := parms.castOn;
  var spellid := parms.spellid;
  var spellcfg := ReadConfigFile(":*:spells");
  var circle := spellcfg[spellid].circle;

  SetObjProperty(castOn, "LastHit", {caster.name, caster.serial, "mana vampire"});
  if(spell_reflect(castOn))
    castOn := caster;
  endif
  if(!castOn.isA(POLCLASS_MOBILE))
    return;
  endif
  var dmg := Cint(GetAttribute(caster, "magery") / 3);
  var diff := Resisted(circle, caster, castOn, dmg);
  if(diff)
    PlaySoundEffect(caster, SFX_SPELL_MANA_VAMPIRE);
    PlayObjectCenteredEffect(castOn, FX_GLOW, 7,0x10);
    var gain := 0;
	if(diff > GetMana(castOn))
	  gain := GetMana(castOn);
	  SetMana(castOn, 0);
	else
	  gain := diff;
	  SetMana(castOn, GetMana(castOn) - diff);
	endif
	SetMana(caster, GetMana(caster) + gain);
	if(GetMana(caster) > GetMaxMana(caster))
	  SetMana(caster, GetMaxMana(caster));
	endif
  endif

	return;
endprogram
