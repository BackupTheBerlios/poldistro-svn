// //$Id: evalInt.src 834 2005-11-02 14:09:50Z austin $

use uo;

include ":attributes:attributeConstants";
include ":attributes:attributes";
include ":attributes:skillCheck";

program skill_EvaluatingIntelligence(who)

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");

	SendSysMessage(who, "What do you wish to evaluate?");
	var subject := Target(who);
	// target checks begin here
	if(!subject)
		return 0;
	elseif(subject.serial == who.serial)
		PrintTextAbovePrivate(who, "You know your mental capabilities quite well enough already.", who);
		return 0;
	elseif(subject.graphic == 0x2006)
		SendSysMessage(who, "Looks pretty dead to you.");
		return 0;
	elseif(!subject.isA(POLCLASS_MOBILE))
		SendSysMessage(who, "That cannot be evaluated.");
		return 0;
	endif

	// variables for sys messages
	var intell := getIntell(AP_GetStat(subject, "Intelligence"), subject.graphic, subject.gender);
	var mental_percentage := CInt(CInt(AP_GetVital(subject, "Mana"))/CDbl(AP_GetVitalMaximumValue(subject, "Mana"))*100);
	var mentalStrength := getMentalStrength(mental_percentage);

	// adjust succeed and fail msgs here
	var strMsg, strFail;
	if(subject.graphic != 0x190 && subject.graphic != 0x191)
		strMsg := "That creature";
		strFail := "that creature's mental abilities.";
	else
		strMsg := subject.name;
		if(upper(subject.name[len(subject.name)]) != "S" || lower(subject.name[len(subject.name)]) != "s" )
			strFail := subject.name + "'s mental abilities.";
		else
			strFail := subject.name + "' mental abilities.";
		endif
	endif
	if(SkillCheck(who, EVALUATING_INTELLIGENCE, -1) > 0)
		PrintTextAbovePrivate(subject, strMsg + " looks " + intell + ".", who);
		// player's evalint skill equal/greater than 76 can get mental strength info
		var skill := AP_GetSkill(who, EVALUATING_INTELLIGENCE);
		if(skill >= 76)
			PrintTextAbovePrivate(subject, strMsg + " is at " + mentalStrength + " mental strength.", who);
		endif
		return 1;
	else
		PrintTextAbovePrivate(subject, "You cannot judge " + strFail, who);
		return 0;
	endif

endprogram


function getIntell(subject, graphic, gender)

	var strSuper, strGender;
	if(graphic != 0x190 && graphic != 0x191)
		strSuper := "supernaturally";
		strGender := "it";
	else
		strSuper := "superhumanly";
		if(gender == 0)
			strGender := "he";
		elseif(gender == 1)
			strGender := "she";
		endif
	endif
	if(subject >= 100)
		return strSuper + " intelligent in a manner you cannot comprehend";
	elseif(subject >= 99)
		return "like a definite genius";
	elseif(subject >= 89)
		return "like a formidable intellect, well beyond even the extraordinary";
	elseif(subject >= 79)
		return "extraordinarily intelligent";
	elseif(subject >= 69)
		return "extremely intelligent";
	elseif(subject >= 59)
		return "very intelligent";
	elseif(subject >= 49)
		return "moderately intelligent";
	elseif(subject >= 39)
		return "about average";
	elseif(subject >= 29)
		return "not the brightest";
	elseif(subject >= 19)
		return "fairly stupid";
	elseif(subject >= 9)
		return "slightly less intelligent than a rock";
	else
		return "smarter than a rock, but dumber than a piece of wood";
	endif

endfunction


function getMentalStrength(subject)

	if(subject >= 100)
		return "one-hundred percent";
	elseif(subject >= 90)
		return "ninety percent";
	elseif(subject >= 80)
		return "eighty percent";
	elseif(subject >= 70)
		return "seventy percent";
	elseif(subject >= 60)
		return "sixty percent";
	elseif(subject >= 50)
		return "fifty percent";
	elseif(subject >= 40)
		return "forty percent";
	elseif(subject >= 30)
		return "thirty percent";
	elseif(subject >= 20)
		return "twenty percent";
	elseif(subject >= 10)
		return "low";
	else
		return "very low";
	endif

endfunction